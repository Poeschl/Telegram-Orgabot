import java.util.regex.Matcher

plugins {
  id 'java'
  id 'idea'
  id 'org.jetbrains.kotlin.jvm' version '1.5.30'
  id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.31'
  id 'com.github.johnrengelman.shadow' version '7.0.0'
  id 'net.researchgate.release' version '2.8.1'
}

group 'xyz.poeschl.bot.rosie'

sourceCompatibility = JavaVersion.VERSION_16

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib'
  implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2"
  implementation 'com.charleskorn.kaml:kaml:0.35.3'
  implementation 'io.github.microutils:kotlin-logging-jvm:2.0.11'
  implementation 'ch.qos.logback:logback-classic:1.2.6'
  implementation 'com.github.elbekD:kt-telegram-bot:1.3.8'
  implementation 'com.google.api-client:google-api-client:1.32.1'
  implementation 'com.google.apis:google-api-services-sheets:v4-rev20210629-1.32.1'
  implementation 'com.github.shyiko.skedule:skedule:0.4.0'


  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

compileKotlin {
  kotlinOptions.jvmTarget = JavaVersion.VERSION_16
}
compileTestKotlin {
  kotlinOptions.jvmTarget = JavaVersion.VERSION_16
}

test {
  useJUnitPlatform()
}

jar {
  manifest {
    attributes 'Main-Class': 'xyz.poeschl.bot.rosie.BotApplicationKt'
  }
}

shadowJar {
  archiveClassifier.set('')
}

release {
  buildTasks = []
  preTagCommitMessage = ':bookmark: pre tag commit: '
  tagCommitMessage = ':bookmark: creating tag: '
  newVersionCommitMessage = ':bookmark: new version commit: '
  git {
    requireBranch = 'master'
  }
  versionPatterns = [
      /(\d+)\.(\d+)\.(\d+)([^\d]*$)/: { Matcher m, Project p -> m.replaceAll("${m[0][1]}.${(m[0][2] as int) + 1}.${m[0][3]}${m[0][4]}") }
  ]
}
